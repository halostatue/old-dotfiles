#!/bin/zsh

zmodload zsh/complist
autoload -U compinit && compinit

if is-mac; then
  fignore+=(DS_Store)
fi
fignore+=(swo swp)

function bash_source()
{
  alias shopt=':'
  alias _expand=_bash_expand
  alias _complete=_bash_comp
  emulate -L sh
  setopt kshglob noshglob braceexpand

  source "$@"
}

# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

zstyle ':completion:*' squeeze-slashes true

# Caching
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ${zsh_cache}/complete-cache

zstyle ':completion:*' add-space true
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list
zstyle ':completion:*' menu select=1
zstyle ':completion:*' file-sort name
zstyle ':completion:*' list-colors ''
#zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' matcher-list 'r:|[._-]=** r:|=**' 'l:|=** r:|=**'
zstyle ':completion:*' menu select
#zstyle ':completion:*' menu 'select=1'
zstyle ':completion:*:approximate:*' max-errors 1 numeric
# Fuzzy matching
zstyle -e ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) numeric )'
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:functions' ignored-patterns '_*'
zstyle ':completion:*:*:(^rm):*:*files' ignored-patterns \
'*?.(o|c~|zwc)' '*?~'

#[ Formats ]####################################################################
zstyle ':completion:*' auto-description 'Specify: %d'
zstyle ':completion:*' group 1
zstyle ':completion:*' format '%B---- %d%b'
zstyle ':completion:*:corrections' format '%B---- %d (errors %e)%b'
zstyle ':completion:*:descriptions' format "%B---- %d%b"
zstyle ':completion:*:messages' format '%B%U---- %d%u%b' 
zstyle ':completion:*:warnings' format "%B$fg[red]%}---- no match for: $fg[white]%d%b"
zstyle ':completion:*' group-name ''
zstyle ':completion:*:-command-:*:(commands|builtins|reserved-words-aliases)' group-name commands

# Separate man page sections
zstyle ':completion:*:manuals' seperate-sections true

# Separate comand line options and descriptions with #
zstyle ':completion:*' list-separator '#'

#[ Kill ]#######################################################################
zstyle ':completion:*:processes' command 'ps -au$USER -o pid,time,cmd|grep -v "ps -au$USER -o pid,time,cmd"'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)[ 0-9:]#([^ ]#)*=01;30=01;31=01;38'

zstyle ':completion:*:cd:*' ignore-parents parent pwd

# Give long completion options in a list. tab to advance.
zstyle ':completion:*:default' list-prompt '%S%M matches%s'

#[ hosts and users ]############################################################
#hosts=()
#[ -r ~/.ssh/config ] && hosts=(${${${$(grep '^Host' ~/.ssh/config)}##Host }##[*0-9]*})
#[ -r ~/.ssh/known_hosts ] && hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[0-9]*}%\*}%,*})
hosts=($((
        ( [ -r ~/.ssh/config ] && awk '/^host +[a-z]/ { print $2 }' ~/.ssh/config) ;\
        ( [ -r ~/.ssh/known_hosts ] && awk '{print $1}' ~/.ssh/known_hosts | tr , '\n')
) | sort -u))

zstyle ':completion:*' hosts $hosts
#zstyle ':completion:*:hosts' list-colors '=(#b)(*)(.jukie.net)=01;30=01;31' '=[^.]#=01;31'

users=(root austin)
zstyle ':completion:*' users $users
# zstyle ':completion:*' users resolve

zstyle ':completion:*:*:[ak]dvi:*' file-patterns \
    '*.dvi:dvi-files:DVI\ files *(-/):directories:Directories' '*:all-files'
zstyle ':completion:*:*:kghostview:*' file-patterns \
    '*.(ps|pdf)(|.gz|.bz2):pspdf-files:PostScript\ or\ PDF\ files  *(-/):directories:Directories' '*:all-files'
zstyle ':completion:*:*:swfplayer:*' file-patterns \
    '*.swf:swf-files:Swf\ files  *(-/):directories:Directories' '*:all-files'

zstyle ':completion:*' file-patterns \
    '%p:globbed-files: *(-/):directories:Directories' '*:all-files'

# only java files for javac
zstyle ':completion:*:javac:*' files '*.java'

#[ ignores for vim ]############################################################

zstyle ':completion:*:vi:*' ignored-patterns '*?.(o|a|so|aux|dvi|log|swp|fig|bbl|blg|bst|idx|ind|out|toc|lot|lof|class|pdf|ps|pyc|cm?)'
zstyle ':completion:*:mate:*' ignored-patterns '*?.(o|a|so|aux|dvi|log|swp|fig|bbl|blg|bst|idx|ind|out|toc|lot|lof|class|pdf|ps|pyc|cm?)'
zstyle ':completion:*:vim:*' ignored-patterns '*?.(o|a|so|aux|dvi|log|swp|fig|bbl|blg|bst|idx|ind|out|toc|lot|lof|class|pdf|ps|pyc|cm?)'
zstyle ':completion:*:gvim:*' ignored-patterns '*?.(o|a|so|aux|dvi|log|swp|fig|bbl|blg|bst|idx|ind|out|toc|lot|lof|class|pdf|ps|pyc|cm?)'

# no binary files for less
zstyle ':completion:*:less:*' ignored-patterns '*.(o|a|so|dvi|fig|out|class|pdf|ps|pyc)'
zstyle ':completion:*:zless:*' ignored-patterns '*.(o|a|so|dvi|fig|out|class|pdf|ps|pyc)'

# pdf for xpdf
zstyle ':completion:*:xpdf:*' files '*.pdf'
# tar files
zstyle ':completion:*:tar:*' files '*.tar|*.tgz|*.tz|*.tar.Z|*.tar.bz2|*.tZ|*.tar.gz'
# latex to the fullest
# for printing
zstyle ':completion:*:xdvi:*' files '*.dvi'
zstyle ':completion:*:dvips:*' files '*.dvi'

zle -C insert-kept-result complete-word _generic
zle -C expand-kept-result complete-word _generic
zle -C _expand_word complete-word _expand_word_and_keep      

zstyle ':completion:*' insert-kept all
zstyle ':completion:insert-kept-result:*' completer _insert_kept
zstyle ':completion:expand-kept-result:*' completer _insert_kept

bindkey '^Xk' insert-kept-result
bindkey '^XK' expand-kept-result

autoload -Uz bashcompinit && bashcompinit

if [[ -e /etc/bash_completion.d/ack-grep ]]; then
  bash_source /etc/bash_completion.d/ack-grep
fi

autoload -Uz is-at-least

if is-at-least 4.3.6; then
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' formats '%b '
  zstyle ':vcs_info:(svn|bzr|cvs):*' branchformat '%b:%r'
  zstyle ':vcs_info:cvs:*' branchformat '%r '
  zstyle ':vcs_info:bzr:*' use-simple true 
  zstyle ':vcs_info:*' enable git cvs svn hg bzr p4
  # vcs_info also needs to be in precmd, see: 10_hooks
  vcs_info
fi
