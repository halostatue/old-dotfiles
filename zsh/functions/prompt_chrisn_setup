#! /bin/zsh
# chrisn prompt theme

prompt_chrisn_help() {
  cat <<'EOF'

  prompt austin

EOF
}

prompt_chrisn_precmd() {
  # print -Pn "\e]0;%n@%m: %d\a"

  local TERMWIDTH
  (( TERMWIDTH = ${COLUMNS} - 1 ))

  # Truncate the path if it's too long.
  PR_FILLBAR=""
  PR_PWDLEN=""

  local promptsize=${#${(%):---(%n@%m:%l)---()--}}
  local pwdsize=${#${(%):-%~}}

  if [[ "${promptsize} + ${pwdsize}" -gt ${TERMWIDTH} ]]; then
    ((PR_PWDLEN=${TERMWIDTH} - ${promptsize}))
  else
    PR_FILLBAR="\${(l.((${TERMWIDTH} - (${promptsize} + ${pwdsize})))..${PR_HBAR}.)}"
  fi
}

prompt_chrisn_preexec() {
  if [[ "${TERM}" == "screen" ]]; then
    local CMD=${1[(wr)^(*=*|sudo|-*)]}
    echo -n "\ek${CMD}\e\\"
  fi
}

prompt_chrisn_setup()
{
  PR_TITLEBAR=''
  PR_STITLE=''

  # Decide if we need to set titlebar text.
  # Decide whether to set a screen title
  case ${TERM} in
    xterm*|ansi)    PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
                    ;;
    screen)         PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
                    PR_STITLE=$'%{\ekzsh\e\\%}'
                    ;;
  esac

  PROMPT="${PR_STITLE}"
  PROMPT+="${(e)PR_TITLEBAR}"
  PROMPT+="%(?..${fg_bold[red]}(%?%) )${reset_color}"

  if [[ -n ${SSH_CONNECTION} ]]; then
    PROMPT+="${fg[blue]}(${fg[green]}%(!.%SROOT%s.%n)${fg[green]}@%m:%l${fg[blue]})"
  else
    PROMPT+="%(!.${fg[blue]}(${fg[green]}%SROOT%s${fg[green]}@%m${fg[blue]}) .)"
  fi

  PROMPT+="${fg[blue]}[${fg[magenta]}%(4~,.../,)%3~${fg[blue]}]"
  PROMPT+="\$(git_info_for_prompt \"${fg[green]}|%s\")"
  PROMPT+="
"
  PROMPT+="%(2L.${fg[blue]}<${fg[green]}%L${fg[blue]}>.)"
  PROMPT+="${fg[cyan]}%h${fg[blue]}%(!.#.$) ${reset_color}"

  PS2="${fg[blue]}(${fg_bold[green]}%_${fg[blue]})${reset_color} "

  export PROMPT PS2
  unset RPS1 RPS2 RPS3 RPS4

  precmd_functions+='prompt_chrisn_precmd'
  preexec_functions+='prompt_chrisn_preexec'
}

prompt_chrisn_setup "$@"
